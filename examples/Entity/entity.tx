/*
  Entity DSL grammar.
*/

EntityModel:
    types*=PrimitiveType    // At the beginning of model we can define
                            // zero or more primitive types.
    entities+=Entity        // Each model has one or more entities.
;

Entity:
    'entity' name=ID '{'
        attributes+=Attribute     // Each entity has one or more attributes.
    '}'
;

Attribute:
    name=ID ':' type=[Type]     // type is a reference to Type instance.
                                // There are two built-in primitive types 
                                // registered on meta-model in entity_test.py
;

// Type can be PrimitiveType or Entity
Type:
    PrimitiveType | Entity
;

PrimitiveType:
    'type' name=ID
;

// Special rule for comments. Comments start with //
Comment:
    /\/\/.*$/
;
